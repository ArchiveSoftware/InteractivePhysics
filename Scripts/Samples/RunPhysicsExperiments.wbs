'
'    RunPhysicsExperiments.WBS
'
'    Written by Michael Woo, October 17th, 2001
'    Portions of code by Tim Loduha
'    Copyright (c) 2006 Design Simulation Technologies, Inc.
'    Portions Copyright (c) 2001-2005 MSC.Software Corporation
'

Begin Dialog RunPhysicsExperiments ,,150,80,"Run Physics Experiments"
   OKButton 24,52,40,14
   CancelButton 80,52,40,14
   OptionGroup .OptionGroup1
   OptionButton 8,12,104,8,"Run all physics experiments",.RunAll
   OptionButton 8,28,140,12,"Select a physics topic",.RunSelect
End Dialog

Dim Directories$()


Function Handle(ControlName$, Action%, SuppValue%) as Integer
'-------------------------
' Handles dialog commands.
'-------------------------
   Select Case Action
   Case 1
      'Gives dialog box initial values
      If basic.os = ebWin16 or basic.os = ebWin32 then
         chdir(WM.StartupDirectory)
         chdir("..")
         chdir("PhysicsExperiments")
'        msgbox("current directory " & curdir$)
         GetFiles
      Else
'        msgbox(CurDir$)
         GetFilesMac
      End if

      'Refresh ListBoxes
      DlgListBoxArray "Directories", Directories
	
   Case 2
      'Handles dialog commands
'      If DlgFocus$() = "Directories" Then
'         If basic.os = ebWin16 or basic.os = ebWin32 then
'            ChangeDirs
'         Else
'            ChangeDirsMac
'         End if
'         Handle = 1
'      Else
      If ControlName$ = "OK" And DlgFocus$() = "Directories" Then
         If basic.os = ebWin16 or basic.os = ebWin32 then
            ChangeDirs
         Else
            ChangeDirsMac
         End if
         Handle = 1
      ElseIf ControlName$ = "Cancel" Then
         Stop	
      End If

   End Select
End Function


Sub ChangeDirs
'------------------------------------------------------------------------------
' Updates the current directory and calls GetFiles to fill the Directory and 
' ChooseFiles arrays with new values. 
'------------------------------------------------------------------------------
   'Check to see if it is ".."
   If DlgText$("Directories") = "[Go up one directory]" Then
      Direct$ = ".."
      ChDir(Direct)

   'Change drive
   ElseIf Left(DlgText$("Directories"),1) = "[" Then
      If basic.os = ebWin16 or basic.os = ebWin32 then	
         '**Need to check for disk in drive
         Strip$ = Mid$(DlgText$("Directories"),2,1)
         ChDrive(Strip)
      End if
		
   'Change directories
   ElseIf Right$(CurDir$,1) = "\" Then
      Direct$ = CurDir$ + DlgText$("Directories")
      ChDir(Direct)
   Else
      Direct = CurDir$ + "\" + DlgText$("Directories")
      ChDir(Direct)	
   End If
	
   'Fill arrays with new names
   GetFiles

   'Update
   DirectoryPath = CurDir$
'   MsgBox(DirectoryPath)
   DirectoryPathLength = Len(DirectoryPath)
'  MsgBox(DirectoryPathLength)
   FirstPart = Mid(DirectoryPath,1,40)
   SecondPart = Mid(DirectoryPath,41,DIrectoryPathLength)
   ThirdPart = Mid(DirectoryPath,81,DirectoryPathLength)
'  DlgText "CurrentDir", CurDir$
   DlgText "CurrentDir", FirstPart + Chr$(13) + Chr$(10) + SecondPart + Chr$(13) + Chr$(10) + ThirdPart
   DlgListBoxArray "Directories", Directories

   'Highlights the top item in directories after a change
   DlgText "Directories", Directories(Lbound(Directories))
End Sub


Sub ChangeDirsMac
'------------------------------------------------------------------------------
' Updates the current directory and calls GetFiles to fill the Directory and 
' ChooseFiles arrays with new values. Specific for Macs.
'------------------------------------------------------------------------------
 
   If DlgText$("Directories") = "[Go up one folder]" then
      Direct = CurDir$ + "::"
   Else   
      Direct = CurDir$ + ":" + DlgText$("Directories")
   End if
   ChDir(Direct)	
 	
   'Fill arrays with new names
   GetFilesMac

   'Update
   DirectoryPath = CurDir$
   DirectoryPathLength = Len(DirectoryPath)
   FirstPart = Mid(DirectoryPath,1,40)
   SecondPart = Mid(DirectoryPath,41,DIrectoryPathLength)
   ThirdPart = Mid(DirectoryPath,81,DirectoryPathLength)
'  DlgText "CurrentDir", CurDir$
   DlgText "CurrentDir", FirstPart + Chr$(13) + Chr$(10) + SecondPart + Chr$(13) + Chr$(10) + ThirdPart
   DlgListBoxArray "Directories", Directories

   'Highlights the top item in directories after a change
   DlgText "Directories", Directories(Lbound(Directories))
End Sub


Sub GetFiles
'-------------------------------------------------------------
' Fills the	Directory and ChooseFiles arrays with new values.
'-------------------------------------------------------------
   Dim Temp$()

   'Get list of directories and disk drives and alphabetize them
   FileDirs Directories
   ArraySort Directories
   DiskDrives Temp
   ArraySort Temp

   'Add ".." to directories list
   If Directories(Lbound(Directories)) <> "." Then
      Redim Preserve Directories(Lbound(Directories) To Ubound(Directories) + 1)
      For i = Ubound(Directories) To Lbound(Directories) + 1 Step -1
         Directories(i) = Directories(i - 1)
      Next i
      If basic.os = ebWin16 or basic.os = ebWin32 then	
         Directories(Lbound(Directories)) = ".."
      End if
   Else
      For i = Lbound(Directories) To Ubound(Directories) - 1
         Directories(i) = Directories(i + 1)
         If Directories(i) = ".." then
            Directories(i) = "[Go up one directory]"
         End if
      Next i
      Redim Preserve Directories(Lbound(Directories) To Ubound(Directories) - 1)
   End If

   'Add brackets to drives if it is on the Windows platform
   If basic.os = ebWin16 or basic.os = ebWin32 then	
      For i = Lbound(Temp) To Ubound(Temp)
         Temp(i) = "[" & Temp(i) & ":\]"
      Next i
   End if

   'Put directories and drives together in one array
   If ArrayDims(Directories) <> 0 And ArrayDims(Temp) <> 0 Then
      Redim Preserve Directories(Lbound(Directories) To Ubound(Directories) + Ubound(Temp) + 1) 
      For i = Lbound(Temp) To Ubound(Temp)
         Directories(Ubound(Directories) - Ubound(Temp) + i) = Temp(i)
      Next i
   ElseIf ArrayDims(Directories) = 0 And ArrayDims(Temp) <> 0 Then
      FileDirs Directories		
   End If			

End Sub	


Sub GetFilesMac
'-------------------------------------------------------------
' Fills the	Directory and ChooseFiles arrays with new values.
'-------------------------------------------------------------

   'Get list of directories and alphabetize them

   FileDirs Directories
   ArraySort Directories

   If ArrayDims(Directories)>0 then   
      Redim Preserve Directories(Lbound(Directories) To Ubound(Directories) + 1)
      For i = Ubound(Directories) To Lbound(Directories) + 1 Step -1
         Directories(i) = Directories(i - 1)
      Next i
      Directories(Lbound(Directories)) = "[Go up one folder]"
   Else
      Redim Preserve Directories(1)
      Directories(Lbound(Directories)) = "[Go up one folder]"
   End if		

End Sub


Sub LaunchRun(ByVal aDoc as String)
'-------------------------------------------------------------
' Launch one simulation
'-------------------------------------------------------------

   Dim MyDoc as WMDocument

   If GetAttr(aDoc) <> ebNormal then
      SetAttr aDoc, ebNormal
   End If
   Set MyDoc = WM.Open(aDoc)
   If MyDoc.PauseControlCount = 0 then
      MyDoc.Run 200
   ElseIf MyDoc.GetPauseControlType(1) <> "loop" then
      MyDoc.Run
   Else
      MyDoc.SetPauseControlType 1, "pause"
      MyDoc.Run
      MyDoc.SetPauseControlType 1, "loop"
      MyDoc.Reset
      MyDoc.Save
   End If
   MyDoc.Close

End Sub


Sub LaunchRuns(ByVal aPath as String)
'-------------------------------------------------------------
' Run simulations
'-------------------------------------------------------------

   Dim separator as String    'Path separator, either : or \
   Dim fNames() as String     'Array of strings of file names
   Dim dNames() as String     'Array of strings of directory names
   Dim namei    as String	'File name


   'Set the current directory to the path
   ChDir(aPath)


   'List of files in current directory               
   If basic.os = ebWin16 or basic.os = ebWin32 then			'Check whether it is Windows or Mac
      FileList fNames,"*.??" 
      If( ArrayDims(fNames)>0 ) Then  
         For i = LBound(fNames) to Ubound(fNames)
           namei = fNames(i)
           If(FileParse$(namei,5) = "ip" or FileParse$(namei,5) = "IP") then 			'Must be .ip extension
               LaunchRun namei      
            End If
         Next i
      End If 
      separator = Basic.PathSeparator$
      FileDirs dNames, "???*"    'Avoid . and .. directories (>=3 char directory names)
      If( ArrayDims(dNames)>0 ) Then  
         For i = LBound(dNames) to UBound(dNames)
            namei = dNames(i)
            LaunchRuns (aPath+separator+namei)
         Next i
      End If
   else										'For Mac computers
      FileList fNames
      If( ArrayDims(fNames)>0 ) Then  
         For i = LBound(fNames) to Ubound(fNames)
            namei = fNames(i)
            LaunchRun namei
         Next i
      End If
      separator = Basic.PathSeparator$
      FileDirs dNames
      If( ArrayDims(dNames)>0 ) Then  
         For i = LBound(dNames) to UBound(dNames)
            namei = dNames(i)
            LaunchRuns (aPath+separator+namei)
         Next i
      End If
   End if 
End Sub

Sub FindPhysicsExperimentsDirectory
'-------------------------------------------------------------
' Locates the absolute path of PhysicsExperiments directory
'-------------------------------------------------------------
   If basic.os = ebWin16 or basic.os = ebWin32 then	
	chDrive(WM.StartupDirectory)
      chDir(WM.StartupDirectory)
      chDir("..")
      chDir("PhysicsExperiments")
   else 
      wmdirectory = CurDir$
      chDir(wmdirectory)
      chDir("PhysicsExperiments")
   End if

End Sub


'----------------Main()---------------------------------------

Sub Main()

   'Original Directory when script was launched.'
   Dim LaunchDirectory as String  
   Dim Directory$()

   Dim PhysicsDialog as RunPhysicsExperiments

   If (Dialog(PhysicsDialog) = 0) then			'Stops function if user clicks Cancel
      Stop
   End if

   If(PhysicsDialog.OptionGroup1=0) then	'Play all physics experiments
      FindPhysicsExperimentsDirectory
      originalDirectory = CurDir$				'Save current directory
      LaunchRuns(originalDirectory)				'Open and run simulations
      ChDir(originalDirectory)				'Reset original directory
      Close  							'Close all open files
   End if

   FindPhysicsExperimentsDirectory

   If(PhysicsDialog.OptionGroup1=1) then		'Play only files from the selected folder
      Begin Dialog SelectPhysicsExperiments ,,184,154,"Select a physics topic and click OK",.Handle
         Text 8,4,96,8,"Selected folder or directory:"
         Text 8,16,174,24,CurDir$,.CurrentDir
         ListBox 8,46,120,104,Directories$,.Directories
         OKButton 136,46,40,14,.OK
         CancelButton 136,66,40,14,.Cancel
      End Dialog
      Dim Batch as SelectPhysicsExperiments
      Dialog Batch
	
      LaunchDirectory = CurDir$
      LaunchRuns(LaunchDirectory)
      ChDir(LaunchDirectory)
	Close
   End if
          
End Sub